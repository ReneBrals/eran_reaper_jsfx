desc: A tool for practicing rhythm. Filters notes which are aligned to a groove.
//author: Eran Talmor (C) 2021
slider1:3<1,16,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Beats
slider2:1<1,16,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Beat Sub Divisions
slider3:0<-0.5,0.5,0.01>Swing
slider4:-0.2<-1,0,0.01>Early Filter %
slider5:0.2<0,1,0.01>Late Filter %
slider6:0<0,1,1{Off, On}>Auto Adjust
@init
note_on = $x90;

max_hist = 100;
max_age = 30;
level = 5;


// Relative time to the beat
function getRelTime(offset) local(p)
(
  p = (beat_position + (offset * (tempo / 60) / srate))/beats;
  p - floor(p);
);

function adjustGrid() local(j)
(
  j=0;
  loop(grid_lines+1,
    v_grid[j] = j/grid_lines + (j&(1+grid_lines)&1 ? swing/grid_lines);
    v_early[j] = max(0, v_grid[j] + early/2/grid_lines);
    v_late[j] = min(1, v_grid[j] + late/2/grid_lines);
    j+=1;
  );  
);

function isHit(reltime) local(i result)
(
  i=0;
  result = 0;
  while(
     result |= (v_early[i] <= reltime) && (reltime <= v_late[i]);
     i += 1;
     !result && (i <= grid_lines)
  );
  result;
);

@slider
beats = slider1+1;
subdivisions = slider2 + 1;
grid_lines = beats * subdivisions;
swing = slider3;
early = slider4;
late = slider5;
autoadjust = slider6;

v_history_time = 0;
v_history_rel_time = v_history_time + max_hist;
v_history_hits = v_history_rel_time + max_hist;
histidx = 0;

v_early = max_hist*3;
v_late = v_early + grid_lines + 1;
v_grid = v_late + grid_lines + 1;

v_early[0] = 0;
v_grid[0] = 0;
v_grid[grid_lines] = 1;
v_late[grid_lines] = 1;

adjustGrid();

@block


function addToHistory(reltime, hit)
(
     v_history_time[histidx] = time_precise();
     v_history_rel_time[histidx] = reltime;
     v_history_hits[histidx] = hit;
     histidx += 1;
     (histidx >= max_hist) ? (histidx = 0);
);

while (midirecv(offset,msg1,msg2,msg3)) ( // REAPER 4.59+ syntax while()
   noteStatus = msg1 & $xF0;
   channel = msg1 & 0x0F;
   
   play_state && noteStatus==note_on && msg3!=0 ? (
     note = msg2;
     reltime = getRelTime(offset);
     hit = isHit(reltime);
     hit ? (midisend(offset,msg1,note,msg3));
     addToHistory(reltime, hit);
   ) : (
     midisend(offset,msg1,msg2,msg3); // passthrough other events
   )
);

@gfx
function rgb(r,g,b) 
(
  gfx_r = r;
  gfx_g = g;
  gfx_b = b;
);

function verticalDashedLine(x, y1, y2, dash, space) local(i, y)
(
  y = y1;
  loop ((y2-y1)/(dash + space),
    gfx_line(x,y, x, y+dash);
    y += dash+space);
);

function intervalPassed(k) local(t, res)
(
  t = time();
  res = t - lastupdate > k;
  res ? (lastupdate = t);
  res
);

function adjustPrecision(hits, events)
(
  events > 0 ? (
    hitRatio = hits/events;
    (hitRatio > 0.9) ? level = min(99, level + 1);
    (hitRatio < 0.85) ? level = max(0, level - 5);
    late = (100-level)/100;
    early = -late;
    slider4 = early;
    slider5 = late;
    adjustGrid();
  );   
);

xmargin = 20;
ymargin = 40;
width = gfx_w-xmargin*2;
height = max(150, gfx_h-ymargin*2);
rgb(0.27, 0.27, 0.27); // grey
gfx_rect(xmargin, ymargin, width, height);

// Draw "grid" area
i=0;
loop(grid_lines+1,
  rgb(0, 0.4, 0);
  low = xmargin+v_early[i]*width;
  grid = xmargin+v_grid[i]*width;
  high = xmargin+v_late[i]*width+1;
  gfx_rect(low, ymargin, high-low, height);
  (i % subdivisions) ? 
    (rgb(0.67, 0.67, 0.67); verticalDashedLine(grid, ymargin, ymargin+height, 1, 10))
  : (rgb(1, 1, 1); gfx_line(grid, ymargin, grid, ymargin + height));
  i+=1;
);
gfx_line(gfx_w-xmargin, ymargin, gfx_w-xmargin, ymargin+height);

play_state ? (  
  // Draw history of notes played
  i=0;
  now = time_precise();
  hits = 0;
  events = 0;
  loop(max_hist,
    birth = v_history_time[i];
    birth != 0 ? (
      hit = v_history_hits[i];
      stillHit = isHit(v_history_rel_time[i]);
      age = min(max_age, now - birth);
      ageRatio = 0.2 + 1/(0.8*(1+age));
      stillHit ? (hits += 1);
      hit ? rgb(0,ageRatio,0) : rgb(ageRatio,0,0);
      events += 1;
      x = width * v_history_rel_time[i];
      y = age*20;
      y < height ? (gfx_circle(xmargin + x, ymargin+y, 8, 1));
      age == max_age ? (v_history_time[i] = 0);
    );
    i += 1;
  );
  
  (autoadjust && intervalPassed(1)) ? (adjustPrecision(hits, events));
  
  // Draw vertical line position
  rgb(1,1,0);
  x = xmargin + width * getRelTime();
  gfx_line(x, ymargin, x, ymargin+height);
  
  previousTime = currentTime;
);
